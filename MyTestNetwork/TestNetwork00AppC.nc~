/**
 * TestNetwork00C exercises the basic networking layers, collection and
 * dissemination. The application samples DemoSensorC at a basic rate
 * and sends packets up a collection tree. The rate is configurable
 * through dissemination.
 *
 * See TEP118: Dissemination, TEP 119: Collection, and TEP 123: The
 * Collection Tree Protocol for details.
 * 
 * @author Philip Levis
 * @version $Revision: 1.6 $ $Date: 2007/09/14 18:48:51 $
 */
#include "TestNetwork00.h"
#include "Ctp.h"

configuration TestNetwork00AppC {}
implementation {
  components TestNetwork00C, MainC, LedsC, ActiveMessageC;
  components DisseminationC;
  components new DisseminatorC(uint16_t, SAMPLE_RATE_KEY) as Object16C;
  components CollectionC as Collector;
  components new CollectionSenderC(CL_TEST);
  components new TimerMilliC();
  components new DemoSensorC();
  components new SerialAMSenderC(CL_TEST);
  components SerialActiveMessageC;
#ifndef NO_DEBUG
  components new SerialAMSenderC(AM_COLLECTION_DEBUG) as UARTSender;
  components UARTDebugSenderP as DebugSender;
#endif
  components RandomC;
  components new QueueC(message_t*, 12);
  components new PoolC(message_t, 12);

  TestNetwork00C.Boot -> MainC;
  TestNetwork00C.RadioControl -> ActiveMessageC;
  TestNetwork00C.SerialControl -> SerialActiveMessageC;
  TestNetwork00C.RoutingControl -> Collector;
  TestNetwork00C.DisseminationControl -> DisseminationC;
  TestNetwork00C.Leds -> LedsC;
  TestNetwork00C.Timer -> TimerMilliC;
  TestNetwork00C.DisseminationPeriod -> Object16C;
  TestNetwork00C.Send -> CollectionSenderC;
  TestNetwork00C.ReadSensor -> DemoSensorC;
  TestNetwork00C.RootControl -> Collector;
  TestNetwork00C.Receive -> Collector.Receive[CL_TEST];
  TestNetwork00C.UARTSend -> SerialAMSenderC.AMSend;
  TestNetwork00C.CollectionPacket -> Collector;
  TestNetwork00C.CtpInfo -> Collector;
  TestNetwork00C.CtpCongestion -> Collector;
  TestNetwork00C.Random -> RandomC;
  TestNetwork00C.Pool -> PoolC;
  TestNetwork00C.Queue -> QueueC;
  TestNetwork00C.RadioPacket -> ActiveMessageC;
  
#ifndef NO_DEBUG
  components new PoolC(message_t, 10) as DebugMessagePool;
  components new QueueC(message_t*, 10) as DebugSendQueue;
  DebugSender.Boot -> MainC;
  DebugSender.UARTSend -> UARTSender;
  DebugSender.MessagePool -> DebugMessagePool;
  DebugSender.SendQueue -> DebugSendQueue;
  Collector.CollectionDebug -> DebugSender;
  TestNetwork00C.CollectionDebug -> DebugSender;
#endif
  TestNetwork00C.AMPacket -> ActiveMessageC;
}
